plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group = 'com.example.sportclub'
version = '2.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

javafx {
    version = "21"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

repositories {
    mavenCentral()
}

ext {
    versions = [
            log4j          : '2.20.0',
            jackson        : '2.15.2',
            junit          : '5.10.0',
            assertj        : '3.24.2',
            mockito        : '5.7.0',
            googleJavaFormat: '1.19.2',
            jacoco         : '0.8.11',
            checkstyle     : '10.12.5',
            lombok         : '1.18.30',
            mapstruct      : '1.5.5.Final',
            javafx         : '21.0.1'
    ]
}

dependencies {
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.0'
    compileOnly "org.projectlombok:lombok:${versions.lombok}"
    annotationProcessor "org.projectlombok:lombok:${versions.lombok}"

    implementation "org.mapstruct:mapstruct:${versions.mapstruct}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${versions.mapstruct}"

    implementation "org.apache.logging.log4j:log4j-api:${versions.log4j}"
    implementation "org.apache.logging.log4j:log4j-core:${versions.log4j}"

    implementation "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${versions.jackson}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:${versions.jackson}"

    implementation 'org.openjfx:javafx-controls:21'
    implementation 'org.openjfx:javafx-fxml:21'

    testImplementation "org.junit.jupiter:junit-jupiter-api:${versions.junit}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${versions.junit}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"

    testImplementation "org.assertj:assertj-core:${versions.assertj}"
    testImplementation "org.mockito:mockito-core:${versions.mockito}"
    testImplementation "org.mockito:mockito-junit-jupiter:${versions.mockito}"

    testCompileOnly "org.projectlombok:lombok:${versions.lombok}"
    testAnnotationProcessor "org.projectlombok:lombok:${versions.lombok}"
}

application {
    mainClass = 'sportclub.SportClubApplication'
    mainModule = 'sportclub.app'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-Xlint:unchecked', '-Xlint:deprecation', '-parameters']

    options.annotationProcessorPath = configurations.annotationProcessor
    options.compilerArgs += [
            '-Amapstruct.defaultComponentModel=spring',
            '-Amapstruct.unmappedTargetPolicy=IGNORE'
    ]
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

checkstyle {
    toolVersion = versions.checkstyle
    configFile = file("${rootDir}/config/checkstyle/google_checks.xml")
    maxErrors = 0
    ignoreFailures = false
    showViolations = true
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
        html.outputLocation = file("${buildDir}/reports/checkstyle/main.html")
    }
    exclude '**/test/**'
    classpath = files()
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport

    testLogging {
        events "passed", "skipped", "failed"
        showExceptions = true
        exceptionFormat = 'full'
        showStandardStreams = true
    }

    timeout = java.time.Duration.ofMinutes(5)
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

jacoco {
    toolVersion = versions.jacoco
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/SportClubApplication.class',
                    '**/*GUI*.class',
                    '**/*Test*.class',
                    '**/*$*.class'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                minimum = 0
            }
        }
        rule {
            element = 'CLASS'
            includes = ['sportclub.*']
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0
            }
            excludes = [
                    '**SportClubApplication',
                    '**GUI',
                    '**$*'
            ]
        }
    }
}

spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat(versions.googleJavaFormat).aosp()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
        targetExclude('**/build/**', '**/build-*/**')
    }

    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
    enforceCheck = true
}

tasks.named('compileJava') {
    dependsOn 'spotlessApply'
}

tasks.named('compileTestJava') {
    dependsOn 'spotlessApply'
}

tasks.named('check') {
    dependsOn jacocoTestCoverageVerification, 'checkstyleMain', 'checkstyleTest', 'spotlessCheck'
}

tasks.named('build') {
    dependsOn 'check', 'jacocoTestReport'
}
